// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        minSdkVersion = 21
        compileSdkVersion = 29
        targetSdkVersion = 29
        kotlinVersion = "1.3.50"
        excludeAppGlideModule = true
        playServicesVersion = "16.1.0"
        androidXCore = "1.6.0"
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.fabric.io/public"
        }
        maven {
            url "https://packages.rnd.mendix.com/jcenter"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.4.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        apply from: file("./mendixnative-release/mendix.gradle"); applyMendixClassPaths(project)

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        all { ArtifactRepository repo ->
            println repo.url.toString()
            if (repo.url.toString().startsWith("https://jcenter.bintray.com/")) {
                project.logger.warn "Repository ${repo.url} removed."
                remove repo
                mavenCentral()
            }
        }
        google()
        mavenCentral()
        maven { url "https://www.jitpack.io" }
        maven {
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            url "$rootDir/../node_modules/jsc-android/dist"
        }
        maven {
            url "https://maven.fabric.io/public"
        }
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven { url "https://packages.rnd.mendix.com/jcenter" }
    }
    configurations.all {
        resolutionStrategy {
            // Can be removed when upgraded to any version 0.64.4+, as a proper fix is included in react-native itself.
            force "com.facebook.react:react-native:0.61.5"
        }
    }
}
